expr				: KEYWORD:aldagai IDENTIFIER EQ expr
						: comp-expr ((KEYWORD:AND|KEYWORD:OR) comp-expr)*

comp-expr		: NOT comp-expr
						: arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)*

arith-expr	:	term ((PLUS|MINUS) term)*

term				: factor ((MUL|DIV) factor)*

factor			: (PLUS|MINUS) factor
						: power

power				: atom (POW factor)*

atom 				: INT|FLOAT|IDENTIFIER
						: LPAREN expr RPAREN
						: if-expr
						: for-expr
						: while-expr

if-expr			: KEYWORD:bada expr KEYWORD:gevo expr
							(KEYWORD:elif expr KEYWORD:gevo expr)*
							(KEYWORD:bestela expr)?

for-expr		: KEYWORD:FOR IDENTIFIER EQ expr KEYWORD:TO expr 
							(KEYWORD:STEP expr)? KEYWORD:gevo expr

while-expr	: KEYWORD:bitartean expr KEYWORD:gevo expr
